plugins {
    id 'org.springframework.boot' version "${springboot_version}"
    id 'java'
    id 'checkstyle'
    id "org.jetbrains.kotlin.jvm" version "${kotlin_version}"
    id "io.gitlab.arturbosch.detekt" version "${detekt_version}"
}

group = 'com.exactpro.th2'
version = release_version

apply plugin: 'distribution'

repositories {
    mavenCentral()
}

configurations.all {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    exclude group: 'org.springframework.boot', module: 'logback-classic'
}

repositories {
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
}

checkstyle {
    toolVersion = "8.26"
}

detekt {
    buildUponDefaultConfig = true
    autoCorrect = true
    config = files("$rootDir/config/detekt/detekt.yml")
}

ext {
    fabric8_version = '6.1.1'
    infra_repo_version = '1.1.17-infra-2.0-3104178934-SNAPSHOT'
    schema_validator_version = '1.5.1-infra-2.0-3135518601-SNAPSHOT'
    jackson_dataformat_version = '2.14.0-rc1'
    jackson_kotlin_version = '2.13.2'
    zip4j_version = '2.11.2'
    commons_text_version = '1.9'
    rxjava_version = '1.3.8'
    slf4j_api_version = '1.7.36'
    log4j_slf4j_impl_version = '2.18.0'
    prometheus_version = '0.16.0'
    jupiter_version = '5.9.0'
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:${springboot_version}")

    implementation "com.exactpro.th2:infra-repo:${infra_repo_version}"

    implementation "com.exactpro.th2:schema-validator:${schema_validator_version}"

    implementation "io.fabric8:kubernetes-client:${fabric8_version}"

    implementation "net.lingala.zip4j:zip4j:${zip4j_version}"

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jackson_dataformat_version}"
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: "${jackson_kotlin_version}"
    implementation "org.apache.commons:commons-text:${commons_text_version}"
    implementation "io.reactivex:rxjava:${rxjava_version}"

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation "org.slf4j:slf4j-api:${slf4j_api_version}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_slf4j_impl_version}"

    implementation "io.prometheus:simpleclient:${prometheus_version}"
    implementation "io.prometheus:simpleclient_httpserver:${prometheus_version}"
    implementation "io.prometheus:simpleclient_hotspot:${prometheus_version}"

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation("org.junit.jupiter:junit-jupiter-api:${jupiter_version}")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:${jupiter_version}")

    detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:${detekt_version}")
}

bootJar {
    exclude('log4j.properties')
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

jar {
    enabled = false
}

bootJar{
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}
